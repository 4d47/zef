#! perl6
use v6;

use Zef::App;
use Zef::Utils::SystemInfo;

my $app = Zef::App.new;

#| Download specific distributions
multi MAIN('get', Bool :$depends, Bool :$test-depends, Bool :$build-depends, *@identities) {
    $app.get(|@identities, :$depends, :$test-depends, :$build-depends);
}

#| Run tests
multi MAIN('test', *@paths) {
    my %results = $app.test(|@paths);
    %results<fail>.elems ?? exit(1) !! exit(0);
}

#| Install
multi MAIN('install', Bool :$depends = True, Bool :$test-depends = True, Bool :$build-depends = True,
                      Bool :v($verbose), Bool :$force, Bool :$skip-tests, :$install-to = ['site'], *@identities) {
    $app.install(|@identities, :$depends, :$test-depends, :$build-depends, :$force, :$install-to, :fetch, :test(!$skip-tests));
}

#| Find that shit
multi MAIN('search', *@identities, *%fields) {
    say $app.search(|@identities, |%fields);
}

multi MAIN(Bool :$help?) {
    note q:to/END_USAGE/
        Zef - Perl6 Module Management

        USAGE

          zef [flags|options] command [package]


        COMMANDS

         install                Install specific dependencies by name or path
         test                   Run tests on a given module's path
         get                    Fetch and extract module's source


        OPTIONS

         --install-to=[name]    Short name of CompUnit::Repository to install to


        FLAGS

         --force                Continue each phase regardless of failures
         --skip-tests           Skip the testing phase

         --/depends             Do not fetch dependencies
         --/test-depends        Do not fetch test dependencies
         --/build-depends       Do not fetch build dependencies
        END_USAGE
}
